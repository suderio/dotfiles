#+title: Suderio's GNU Emacs Config
#+AUTHOR: Paulo Suderio
#+DESCRIPTION: My personal Emacs config.
#+STARTUP: showeverything
#+OPTIONS: toc:2
#+PROPERTY: header-args    :tangle yes


* Emacs config :TOC:
  - [[#functions][Functions]]
  - [[#basic][Basic]]
  - [[#ui][UI]]
  - [[#org][Org]]
  - [[#magit][Magit]]
  - [[#languages][Languages]]
  - [[#biblio][Biblio]]
  - [[#lsp][LSP]]
  - [[#latex][Latex]]
  - [[#remember][Remember]]

** Functions

*** Check if font is installed and change
#+begin_src emacs-lisp
(defun sud-font-installed? (font-name)
  "Retorna t se a fonte font-name está instalada"
  (if (find-font (font-spec :name font-name))
       t nil))
#+end_src
*** Create interactive commands from every executable in directory

An idea taken from [[https://pages.sachachua.com/.emacs.d/#scan-bin-and-turn-the-scripts-into-interactive-commands][sachachua]]
#+begin_src emacs-lisp
(after! dash
  (defmacro sud/convert-shell-scripts-to-interactive-commands (directory)
    "Make the shell scripts in DIRECTORY available as interactive commands."
    (cons 'progn
          (-map
           (lambda (filename)
             (let ((function-name (intern (concat "sud/shell-" (file-name-nondirectory filename)))))
               `(defun ,function-name (&rest args)
                  (interactive)
                  (cond
                   ((not (called-interactively-p 'any))
                    (shell-command-to-string (mapconcat 'shell-quote-argument (cons ,filename args) " ")))
                   ((region-active-p)
                    (apply 'call-process-region (point) (mark) ,filename nil (if current-prefix-arg t nil) t args))
                   (t
                    (apply 'call-process ,filename nil (if current-prefix-arg t nil) nil args))))))
           (-filter (-not #'file-directory-p)
                    (-filter #'file-executable-p (directory-files directory t))))))
  (sud/convert-shell-scripts-to-interactive-commands "~/.local/bin")
)
#+end_src
*** Clones git repo from clipboard and opens dired when finished
#+begin_src emacs-lisp
(defvar sud/git-clone-destination "~/git/")
(defun sud/git-clone-clipboard-url ()
  "Clone git URL in clipboard asynchronously and open in dired when finished."
  (interactive)
  (cl-assert (string-match-p "^\\(http\\|https\\|ssh\\)://" (current-kill 0)) nil "No URL in clipboard")
  (let* ((url (current-kill 0))
         (download-dir (expand-file-name sud/git-clone-destination))
         (project-dir (concat (file-name-as-directory download-dir)
                              (file-name-base url)))
         (default-directory download-dir)
         (command (format "git clone %s" url))
         (buffer (generate-new-buffer (format "*%s*" command)))
         (proc))
    (when (file-exists-p project-dir)
      (if (y-or-n-p (format "%s exists. delete?" (file-name-base url)))
          (delete-directory project-dir t)
        (user-error "Bailed")))
    (switch-to-buffer buffer)
    (setq proc (start-process-shell-command (nth 0 (split-string command)) buffer command))
    (with-current-buffer buffer
      (setq default-directory download-dir)
      (shell-command-save-pos-or-erase)
      (require 'shell)
      (shell-mode)
      (view-mode +1))
    (set-process-sentinel proc (lambda (process state)
                                 (let ((output (with-current-buffer (process-buffer process)
                                                 (buffer-string))))
                                   (kill-buffer (process-buffer process))
                                   (if (= (process-exit-status process) 0)
                                       (progn
                                         (message "finished: %s" command)
                                         (dired project-dir))
                                     (user-error (format "%s\n%s" command output))))))
    (set-process-filter proc #'comint-output-filter)))

#+end_src
*** Copies every DONE task to the dailies file
Taken from [[https://systemcrafters.net/build-a-second-brain-in-emacs/5-org-roam-hacks/][System Crafters]]
#+begin_src emacs-lisp
(defun sud/org-roam-copy-todo-to-today ()
  (interactive)
  (let ((org-refile-keep t) ;; Set this to nil to delete the original!
        (org-roam-dailies-capture-templates
          '(("t" "tasks" entry "%?"
             :if-new (file+head+olp "%<%Y-%m-%d>.org" "#+title: %<%Y-%m-%d>\n" ("Done!")))))
        (org-after-refile-insert-hook #'save-buffer)
        today-file
        pos)
    (save-window-excursion
      (org-roam-dailies--capture (current-time) t)
      (setq today-file (buffer-file-name))
      (setq pos (point)))

    ;; Only refile if the target file is different than the current file
    (unless (equal (file-truename today-file)
                   (file-truename (buffer-file-name)))
      (org-refile nil nil (list "Done!" today-file nil pos)))))

(after! org
(add-to-list 'org-after-todo-state-change-hook
             (lambda ()
               (when (equal org-state "DONE")
                 (sud/org-roam-copy-todo-to-today)))))

#+end_src
** Basic
Some functionality uses this to identify you, e.g. GPG configuration, email clients, file templates and snippets. It is optional.
#+begin_src emacs-lisp
(setq user-full-name "Paulo Suderio"
      user-mail-address "paulo.suderio@gmail.com")
#+end_src
This determines the style of line numbers in effect. If set to `nil', line numbers are disabled. For relative line numbers, set this to `relative'.
#+begin_src emacs-lisp
(setq display-line-numbers-type 'relative
      evil-respect-visual-line-mode t
      which-key-idle-delay 0.8
      which-key-max-description-length 155
      which-key-separator " → "
      which-key-dont-use-unicode nil
      )
#+end_src
*** Keybindings
#+begin_src emacs-lisp
(map! :leader :desc "Open Journal" "n j o" #'org-journal-open-current-journal-file)
#+end_src
*** Dired
#+begin_src emacs-lisp
(setq global-auto-revert-mode-text "󰀘"
      global-auto-revert-non-file-buffers t)
#+end_src
*** Spell
#+begin_src emacs-lisp
  (setenv "LANG" "pt_BR,en_US")
  (setq-default ispell-program-name "hunspell")
  (with-eval-after-load "ispell"
    (setq ispell-really-hunspell t)
    (setq ispell-program-name "hunspell")
    (setq ispell-dictionary "pt_BR,en_US")
    ;; ispell-set-spellchecker-params has to be called
    ;; before ispell-hunspell-add-multi-dic will work
    (ispell-set-spellchecker-params)
    (ispell-hunspell-add-multi-dic "pt_BR,en_US"))
#+end_src
*** Whitespaces
This trims whitespaces at EOL with little intervention.
#+begin_src emacs-lisp
(use-package! ws-butler
  :hook prog-mode-hook)
#+end_src
** UI
*** Theme
#+begin_src emacs-lisp
(setq doom-theme 'modus-vivendi)
(add-to-list 'default-frame-alist '(alpha-background . 85)) ; For all new frames henceforth
(setq modus-themes-bold-constructs t)
(setq modus-themes-italic-constructs t)
(setq modus-themes-prompts '(bold))
;; Important!
(setq modus-themes-scale-headings t)
(setq  modus-themes-variable-pitch-ui t)
#+end_src
*** Bell
Disables annoying bell
#+begin_src emacs-lisp
(setq visible-bell nil)
(setq ring-bell-function 'ignore)
#+end_src
*** Dashboard
#+begin_src emacs-lisp
(setq fancy-splash-image (file-name-concat doom-user-dir "emacs-logo.png"))

(add-to-list '+doom-dashboard-menu-sections
    '("Open Journal"
     :icon (nerd-icons-octicon "nf-oct-note" :face 'doom-dashboard-menu-title)
     :key "SPC n j o"
     :when (featurep! :lang org +journal)
     :face (:inherit (doom-dashboard-menu-title))
     :action org-journal-open-current-journal-file)
)
#+end_src
*** Fonts
Doom exposes five (optional) variables for controlling fonts in Doom:
 - `doom-font' -- the primary font to use
 - `doom-variable-pitch-font' -- a non-monospace font (where applicable)
 - `doom-big-font' -- used for `doom-big-font-mode'; use this for
   presentations or streaming.
 - `doom-symbol-font' -- for symbols
 - `doom-serif-font' -- for the `fixed-pitch-serif' face
See 'C-h v doom-font' for documentation and more examples of what they accept.
#+begin_src emacs-lisp
(if (sud-font-installed? "FiraCode Nerd Font")
    (setq doom-font (font-spec :family "FiraCode Nerd Font" :size 12 :weight 'semi-light)))

(if (sud-font-installed? "FiraCode Nerd Font Propo")
    (setq doom-variable-pitch-font (font-spec :family "FiraCode Nerd Font Propo" :size 13)))

(if (sud-font-installed? "FiraCode Nerd Font Mono")
    (setq doom-big-font (font-spec :family "FiraCode Nerd Font Mono" :size 16 :weight 'bold)))

(if (sud-font-installed? "Noto Serif")
    (setq doom-serif-font (font-spec :family "Noto Serif" :size 12)))
#+end_src

*** Frame
#+begin_src emacs-lisp
(setq frame-title-format
    '((:eval (if (buffer-file-name) (abbreviate-file-name (buffer-file-name)) "%b"))
      (:eval (if (buffer-modified-p) "!")) " (" user-login-name "@" system-name ")"))
#+end_src
** Org
org-directory must be set before org loads!
#+begin_src emacs-lisp
(setq org-directory "~/org/")
(setq org-agenda-files '("inbox.org" "work.org"))

;; Default tags
(setq org-tag-alist '(
                      ;; locale
                      (:startgroup)
                      ("personal" . ?h)
                      ("work" . ?w)
                      (:endgroup)
                      (:newline)
                      ;; misc
                      ("writing")
                      ("review")
                      ("reading")))

;; Org-refile: where should org-refile look?
;;(setq org-refile-targets 'FIXME)

;; Org-roam variables
(setq org-roam-directory "~/org/roam/")
(setq org-roam-index-file "~/org/roam/index.org")
(setq org-roam-dailies-directory "journal/")
;;; Optional variables

;; Advanced: Custom link types
;; This example is for linking a person's 7-character ID to their page on the
;; free genealogy website Family Search.
(setq org-link-abbrev-alist
      '(("family_search" . "https://www.familysearch.org/tree/person/details/%s")
        ("tarefa" . "http://itsmweb.bndes.net/servlet/ViewFormServlet?form=TMS%3ATask&server=itsm.bndes.net&eid=%s")
        ("incidente" . "http://itsmweb.bndes.net/servlet/ViewFormServlet?form=HPD%3AHelp+Desk&server=itsm.bndes.net&eid=%s")
        ))
#+end_src
*** UI Tweaks
#+begin_src emacs-lisp
(setq-default org-startup-indented t
              org-pretty-entities t
              org-use-sub-superscripts "{}"
              org-hide-emphasis-markers t
              org-startup-with-inline-images t
              org-image-actual-width '(300))
(use-package! toc-org
  :commands toc-org-enable
  :init (add-hook 'org-mode-hook 'toc-org-enable))
#+end_src
*** Org Level Headers
#+begin_src emacs-lisp 
(after! org
(custom-set-faces
 '(org-level-1 ((t (:inherit outline-1 :height 1.5))))
 '(org-level-2 ((t (:inherit outline-2 :height 1.4))))
 '(org-level-3 ((t (:inherit outline-3 :height 1.3))))
 '(org-level-4 ((t (:inherit outline-4 :height 1.2))))
 '(org-level-5 ((t (:inherit outline-5 :height 1.1))))))
#+end_src
*** Journal
#+begin_src emacs-lisp
(setq org-journal-dir "~/org/journal/"
      org-journal-file-format "%Y%m.org")
(after! org-journal
  (setq
   org-journal-date-format "%Y-%m-%d (%A)"
   org-journal-enable-agenda-integration t
   org-journal-file-type 'monthly
   org-icalendar-store-UID t
   org-icalendar-include-todo "all"
   org-icalendar-combined-agenda-file "~/org/org-journal.ics" ;; export with (org-icalendar-combine-agenda-files)
))
#+end_src
*** DOING Capture
#+begin_src emacs-lisp
(after! org
        (setq org-capture-templates
              '(("c" "Default Capture" entry (file "inbox.org")
                 "* TODO %?\n%U\n%i")
                ;; Capture and keep an org-link to the thing we're currently working with
                ("r" "Capture with Reference" entry (file "inbox.org")
                 "* TODO %?\n%U\n%i\n%a")
                ;; Define a section
                ("w" "Work")
                ("wm" "Work meeting" entry (file+headline "work.org" "Meetings")
                 "** TODO %?\n%U\n%i\n%a")
                ("wt" "Work task" entry (file+headline "work.org" "Tasks")
                 "** TODO %c\n%U\n[[tarefa:%c][remedy]]\n%?")
                ("wi" "Work incident" entry (file+headline "work.org" "Incidents")
                 "** TODO %c\n%U\n[[incidente:%c][remedy]]\n%?")
                ("wa" "Work adhoc" entry (file+headline "work.org" "Ad hoc")
                 "** TODO %?\n%U\n%i\n%a")
                ("wr" "Work report" entry (file+headline "work.org" "Reports")
                 "** TODO %?\n%U\n%i\n%a")
                )))

(after! org-roam
        (setq org-roam-capture-templates
              '(("d" "default" plain "%?"
                 :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n") :unnarrowed t)

                ("i" "ideas" plain "%?"
                 :target (file+head "%<%Y%m%d%H%M%S>-${slug}.org" "#+title: ${title}\n"))
                ))
        (setq org-roam-dailies-capture-templates
              '(("d" "default" entry "* %<%I:%M %p>: %?"
                 :if-new (file+head "%<%Y-%m-%d>.org" "#+title: %<%Y-%m-%d>\n"))))
        )
#+end_src
*** Workflow
#+begin_src emacs-lisp
(after! org
(setq org-log-done 'time
      org-todo-keywords '((sequence "TODO" "WAITING" "DOING" "|" "DONE(!)" "CANCELLED(!)"))
;; Refile configuration
      org-outline-path-complete-in-steps nil
      org-refile-use-outline-path 'file))
#+end_src
*** Agenda
#+begin_src emacs-lisp
(setq org-agenda-custom-commands
      '(("n" "Agenda and All Todos"
         ((agenda)
          (todo)))
        ("w" "Work" agenda ""
         ((org-agenda-files '("work.org"))))))
#+end_src
*** DOING Export
#+begin_src emacs-lisp
;; Make exporting quotes better
(setq org-export-with-smart-quotes t
      org-export-with-drawers nil
      org-export-with-todo-keywords nil
      org-export-with-broken-links t
      org-export-with-toc nil
      org-export-date-timestamp-format "%d %B %Y")
;; Export ODT to MS-Word
;;(setq-default org-odt-preferred-output-format "docx")
;; Export ODT to PDF
(setq-default org-odt-preferred-output-format "pdf")
#+end_src
*** Org-tempo
Org-tempo is not a separate package but a module within org that can be enabled.  Org-tempo allows for '<s' followed by TAB to expand to a begin_src tag.  Other expansions available include:

| Typing the below + TAB | Expands to ...                          |
|------------------------+-----------------------------------------|
| <a                     | '#+BEGIN_EXPORT ascii' … '#+END_EXPORT  |
| <c                     | '#+BEGIN_CENTER' … '#+END_CENTER'       |
| <C                     | '#+BEGIN_COMMENT' … '#+END_COMMENT'     |
| <e                     | '#+BEGIN_EXAMPLE' … '#+END_EXAMPLE'     |
| <E                     | '#+BEGIN_EXPORT' … '#+END_EXPORT'       |
| <h                     | '#+BEGIN_EXPORT html' … '#+END_EXPORT'  |
| <l                     | '#+BEGIN_EXPORT latex' … '#+END_EXPORT' |
| <q                     | '#+BEGIN_QUOTE' … '#+END_QUOTE'         |
| <s                     | '#+BEGIN_SRC' … '#+END_SRC'             |
| <v                     | '#+BEGIN_VERSE' … '#+END_VERSE'         |
#+begin_src emacs-lisp
(require 'org-tempo)
#+end_src
** Magit
Obs.: diff-refine-hunk may be slow
#+begin_src emacs-lisp
(after! magit
  (setq magit-revision-show-gravatars '("^Author:     " . "^Commit:     ")
        magit-diff-refine-hunk 'all))
#+end_src
** Languages
*** Java
#+begin_src emacs-lisp
(setq! lsp-java-server-install-dir "~/.local/etc/eclipse.jdt.ls")
#+end_src
*** Julia
#+begin_src emacs-lisp
(setq lsp-julia-package-dir nil)
(after! lsp-julia
  (setq lsp-julia-default-environment "~/.julia/environments/v1.11"))
#+end_src
** Biblio
#+begin_src emacs-lisp
 (setq! org-cite-csl-styles-dir "~/org/biblio")

 (setq! citar-bibliography '("~/org/biblio/global.bib"))

#+end_src

** LSP
See https://emacs-lsp.github.io/lsp-mode/tutorials/how-to-turn-off/

#+begin_src emacs-lisp
(use-package! lsp-ui
  :hook (lsp-mode . lsp-ui-mode))
#+end_src
*** Formatting
#+begin_src emacs-lisp
 ;; Disable format-on-save behavior in Emacs Lisp buffers
 ;(setq-hook! 'emacs-lisp-mode-hook +format-inhibit t)

 ;; To permenantly disable a formatter:
 (after! csharp-mode
   (set-formatter! 'csharpier nil))

 ;; To define new formatters:
 ;; From modules/tools/docker/config.el:
 (after! dockerfile-mode
   (set-formatter! 'dockfmt '("dockfmt" "fmt" filepath) :modes '(dockerfile-mode)))

 ;; From modules/lang/sh/config.el:
 (after! sh-script
   (set-formatter! 'shfmt '("shfmt" "-ci"
                            (unless indent-tabs-mode
                              (list "-i" (number-to-string tab-width))))))

(setq +format-on-save-disabled-modes
      '(emacs-lisp-mode  ; elisp's mechanisms are good enough
        sql-mode         ; sqlformat is currently broken
        tex-mode         ; latexindent is broken
        latex-mode))


#+end_src
** Latex

https://github.com/james-stoup/emacs-org-mode-tutorial

#+begin_src emacs-lisp
;(setq reftex-default-bibliography "/your/bib/file.bib")
(use-package! ox-latex
  :ensure nil
  :demand t
  :custom
  ;; Multiple LaTeX passes for bibliographies
  (org-latex-pdf-process
   '("pdflatex -interaction nonstopmode -output-directory %o %f"
     "bibtex %b"
     "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"
     "pdflatex -shell-escape -interaction nonstopmode -output-directory %o %f"))
  ;; Clean temporary files after export
  (org-latex-logfiles-extensions
   (quote ("lof" "lot" "tex~" "aux" "idx" "log" "out"
           "toc" "nav" "snm" "vrb" "dvi" "fdb_latexmk"
           "blg" "brf" "fls" "entoc" "ps" "spl" "bbl"
           "tex" "bcf"))))
(use-package! latex-preview-pane
  :defer t
  :commands  (latex-preview-pane-mode)
  :hook ((latex-mode . latex-preview-pane-mode)))
(use-package! ox-epub
  :demand t)
#+end_src
*** ABNT2
#+begin_src emacs-lisp
(after! ox-latex
        (add-to-list 'org-latex-classes
                     '("abntex2"
"[NO-DEFAULT-PACKAGES]
\\documentclass{abntex2}
\\usepackage{lmodern}
\\usepackage[T1]{fontenc}
\\usepackage[utf8]{inputenc}
\\usepackage{indentfirst}
\\usepackage{nomencl}
\\usepackage{color}
\\usepackage{graphicx}
\\usepackage{microtype}
\\usepackage[brazilian,hyperpageref]{backref}
\\usepackage[alf]{abntex2cite}
\\usepackage{fourier}
[EXTRA]"
                       ("\\section{%s}" . "\\section*{%s}")
                       ("\\subsection{%s}" . "\\subsection*{%s}")
                       ("\\subsubsection{%s}" . "\\subsubsection*{%s}")
                       ("\\paragraph{%s}" . "\\paragraph*{%s}")
                       ("\\subparagraph{%s}" . "\\subparagraph*{%s}")
                       )))

(setq org-latex-hyperref-template
"\\hypersetup{
 pdftitle={%t},
 pdfauthor={%a},
 pdfsubject={%d},
 pdfcreator={%c},
 pdfkeywords={%k},
 pdflang={%L},
 colorlinks=true,
 linkcolor=blue,
 citecolor=blue,
 filecolor=magenta,
 urlcolor=blue,
 bookmarksdepth=4}
")
#+end_src

** Remember
Whenever you reconfigure a package, make sure to wrap your config in an
 `after!' block, otherwise Doom's defaults may override your settings. E.g.
#+begin_src emacs-lisp tangle: no
   (after! PACKAGE
     (setq x y))
#+end_src

 The exceptions to this rule:
   - Setting file/directory variables (like `org-directory')
   - Setting variables which explicitly tell you to set them before their
     package is loaded (see 'C-h v VARIABLE' to look up their documentation).
   - Setting doom variables (which start with 'doom-' or '+').

 Here are some additional functions/macros that will help you configure Doom.

 - `load!' for loading external *.el files relative to this one
 - `use-package!' for configuring packages
 - `after!' for running code after a package has loaded
 - `add-load-path!' for adding directories to the `load-path', relative to
   this file. Emacs searches the `load-path' when you load packages with
   `require' or `use-package'.
 - `map!' for binding new keys

 To get information about any of these functions/macros, move the cursor over
 the highlighted symbol at press 'K' (non-evil users must press 'C-c c k').
 This will open documentation for it, including demos of how they are used.
 Alternatively, use `C-h o' to look up a symbol (functions, variables, faces,
 etc).

 You can also try 'gd' (or 'C-c c d') to jump to their definition and see how
 they are implemented.
