#!/usr/bin/env perl
use strict;
use warnings;
use FindBin qw($Bin);
use Cwd qw(realpath);
use File::Glob qw(bsd_glob);
use File::Spec;
use utf8;
use Time::Piece;
binmode STDOUT, ":utf8";

my $mode = shift @ARGV // '';

# Detecta se terminal suporta cores ANSI
my $USE_COLOR = 1;
if ($ENV{NO_COLOR} || ! -t STDOUT) {
    $USE_COLOR = 0;
}

# Cores ANSI (usadas apenas se $USE_COLOR for verdadeiro)
my $RED    = $USE_COLOR ? "\e[31m" : '';
my $YELLOW = $USE_COLOR ? "\e[33m" : '';
my $GREEN  = $USE_COLOR ? "\e[32m" : '';
my $CYAN   = $USE_COLOR ? "\e[36m" : '';
my $RESET  = $USE_COLOR ? "\e[0m"  : '';

# Verificar se git está disponível
my $git_found = 0;
foreach my $dir (split /:/, $ENV{PATH}) {
    if (-x "$dir/git") {
        $git_found = 1;
        last;
    }
}
die "${RED}\x{26A0} Erro:${RESET} git não encontrado no PATH.\n" unless $git_found;

# Ler .env
my $env_file = "$Bin/.env";
my ($git_dirs, $git_bare_repos, $git_sync_repos, $git_last_check) = ('', '', '');
if (-f $env_file) {
    open my $fh, '<', $env_file or die "${RED}\x{26A0} Erro:${RESET} ao abrir .env: $!\n";
    while (<$fh>) {
        chomp;
        s/#.*//;
        s/^\s+|\s+\$//g;
        $git_dirs = $1 if /^GIT_REPOS\s*=\s*"?(.+?)"?$/;
        $git_bare_repos = $1 if /^GIT_BARE_REPOS\s*=\s*"?(.+?)"?$/;
        $git_sync_repos = $1 if /^GIT_SYNC_REPOS\s*=\s*"?(.+?)"?$/;
        $git_last_check = $1 if /^GIT_LAST_CHECK\s*=\s*"?(.+?)"?$/;
    }
    close $fh;
}

exit 0 unless should_run($git_last_check);

update_git_last_check("$Bin/.env");

# Montar lista de repositórios permitidos para sync
my %sync_allowed;
foreach my $pattern (split /\s+/, $git_sync_repos) {
    $pattern = "$Bin/$pattern" if $pattern !~ m{^[/~]};
    foreach my $match (bsd_glob($pattern)) {
        my $resolved = realpath($match);
        $sync_allowed{$resolved} = 1 if defined $resolved;
    }
}

# Expandir GIT_REPOS
my @repos;
foreach my $pattern (split /\s+/, $git_dirs) {
    $pattern = "$Bin/$pattern" if $pattern !~ m{^[/~]};
    push @repos, grep { -d $_ } bsd_glob($pattern);
}

# Expandir GIT_BARE_REPOS
my @bare_repos;
foreach my $entry (split /\s+/, $git_bare_repos) {
    if ($entry =~ /^([^:]+):(.+)$/) {
        my ($bare, $worktree) = ($1, $2);
        $bare = "$Bin/$bare" if $bare !~ m{^[/~]};
        $worktree = "$Bin/$worktree" if $worktree !~ m{^[/~]};
        foreach my $bare_exp (bsd_glob($bare)) {
            foreach my $wt_exp (bsd_glob($worktree)) {
                 push @bare_repos, [$bare_exp, $wt_exp] if -d $bare_exp && -d $wt_exp;
            }
        }
    }
}

sub should_run {
    my ($datestr) = @_;
    return 1 if !defined($datestr) || $datestr eq '';
    # Verifica se está no formato esperado
    unless ($datestr =~ /^\d{8}$/) {
        return 1;
    }
    # Tenta interpretar como data
    my $input_date = eval { Time::Piece->strptime($datestr, "%Y%m%d") };
    if ($@) {
        return 1;  # string no formato, mas data inválida (ex: 20230230)
    }
    # Data atual no mesmo formato
    my $today = Time::Piece->new->strftime("%Y%m%d");
    return $datestr lt $today ? 1 : 0;
}

sub update_git_last_check {
    my ($env_path) = @_;
    return unless -f $env_path;

    my $today = localtime->strftime('%Y%m%d');
    my $found = 0;
    my @lines;

    # Lê o conteúdo atual do .env
    open my $in, '<', $env_path or die "Não foi possível ler $env_path: $!";
    while (my $line = <$in>) {
        chomp $line;
        if ($line =~ /^GIT_LAST_CHECK\s*=/) {
            push @lines, qq{GIT_LAST_CHECK="$today"};
            $found = 1;
        } else {
            push @lines, $line;
        }
    }
    close $in;

    # Adiciona a variável se não existia
    push @lines, qq{GIT_LAST_CHECK="$today"} unless $found;

    # Escreve de volta
    open my $out, '>', $env_path or die "Não foi possível escrever $env_path: $!";
    foreach my $l (@lines) {
        print $out "$l\n";
    }
    close $out;
}

sub handle_repo {
    my ($dir, $is_bare, $worktree) = @_;
    my @git_base = $is_bare ? ("--git-dir=$dir", "--work-tree=$worktree") : ("-C", $dir);
    my $label = $is_bare ? "$dir (bare)" : $dir;
    my $sync_path = realpath($is_bare ? $dir : $dir);

    print "\n\x{21E8} ${CYAN}Verificando:${RESET} $label\n";

    my $branch = `git @git_base branch --show-current 2>/dev/null`;
    chomp $branch;
    return print "   \x{2757} ${YELLOW}Aviso:${RESET} HEAD destacado ou branch indefinido.\n" unless $branch;

    system("git", @git_base, "fetch", "--quiet") == 0
        or return print "   \x{26A0} ${RED}Erro:${RESET} git fetch falhou.\n";

    #my $can_ff = system("git", @git_base, "merge-base", "--is-ancestor", "HEAD", "origin/$branch") == 0;

    my $merge_base_output = `git @git_base merge-base --is-ancestor HEAD origin/$branch 2>/dev/null`;
    my $merge_base_status = $?;
    my $can_ff = $merge_base_status == 0;
    my @ahead = `git @git_base rev-list origin/$branch..HEAD 2>/dev/null`;
    my @behind = `git @git_base rev-list HEAD..origin/$branch 2>/dev/null`;

    if ($mode eq 'sync') {
        unless ($sync_allowed{$sync_path}) {
            print "   \x{2757} ${YELLOW}Aviso:${RESET} Repositório não autorizado para sync.\n";
            return;
        }

        if (@behind && $can_ff) {
            print "   \x{21E8} ${GREEN}Atualizando via fast-forward...${RESET}\n";
            system("git", @git_base, "merge", "--ff-only", "origin/$branch");
        } elsif (@behind) {
            print "   \x{2757} ${YELLOW}Aviso:${RESET} Existem commits remotos, mas não é possível fast-forward.\n";
        } else {
            print "   \x{21E8} Nenhum commit remoto pendente.\n";
        }

        if (@ahead) {
            print "   \x{21E8} Enviando commits locais para origin/$branch...\n";
            system("git", @git_base, "push", "origin", $branch);
        } else {
            print "   \x{21E8} Nenhum commit local para enviar.\n";
        }
    } else {
        if (@behind) {
            print "   \x{2757} ${YELLOW}Aviso:${RESET} $branch está atrás de origin/$branch: ", scalar(@behind), " commit(s)\n";
            my @log = `git @git_base log --oneline --no-merges HEAD..origin/$branch`;
            print map { "      - $_" } @log;
        } else {
            print "   \x{21E8} $branch está atualizado.\n";
        }
        if (@ahead) {
            print "   \x{2757} ${YELLOW}Aviso:${RESET} Existem commits locais não enviados: ", scalar(@ahead), " commit(s)\n";
        }
    }
}

handle_repo($_, 0, undef) for @repos;
foreach my $bare (@bare_repos) {
    my ($git_dir, $worktree) = @$bare;
    # Expandir corretamente ~ para worktree, se necessário
    # $git_dir = glob($git_dir) if $git_dir =~ /^~/;
    # $worktree = glob($worktree) if $worktree =~ /^~/;
    handle_repo($git_dir, 1, $worktree);
}
